" vim basic {{{
" be iMproved, put this line at TOP of vimrc
set nocompatible
let $VIM = $XDG_CONFIG_HOME . '/vim'
set packpath=$VIM
set runtimepath=$VIM,$VIMRUNTIME,$VIM/after,/usr/share/vim/vimfiles
set backupdir=$XDG_CACHE_HOME/vim/backup
set directory=$XDG_CACHE_HOME/vim/swap
if !has('nvim')
    set viminfo+=n$XDG_CACHE_HOME/vim/viminfo
endif
" }}}
" Vim-Packager {{{
if empty(glob($VIM . '/pack/packager/opt/vim-packager'))
  silent !git clone https://github.com/kristijanhusak/vim-packager
    \ $VIM/pack/packager/opt/vim-packager
  packadd vim-packager
endif
function! s:packager_init(packager) abort
  call a:packager.add('airblade/vim-gitgutter')
  call a:packager.add('ap/vim-buftabline')
  call a:packager.add('baskerville/vim-sxhkdrc')
  call a:packager.add('dense-analysis/ale')
  call a:packager.add('fcpg/vim-altscreen')
  call a:packager.add('fs111/pydoc.vim')
  call a:packager.add('godlygeek/tabular')
  call a:packager.add('jpalardy/vim-slime')
  call a:packager.add('junegunn/fzf')
  call a:packager.add('junegunn/fzf.vim')
  call a:packager.add('kristijanhusak/vim-packager')
  call a:packager.add('lervag/vimtex')
  call a:packager.add('liuchengxu/vim-which-key')
  call a:packager.add('majutsushi/tagbar')
  call a:packager.add('SirVer/ultisnips')
  call a:packager.add('tpope/vim-commentary')
  call a:packager.add('tpope/vim-endwise')
  call a:packager.add('tpope/vim-fugitive')
  call a:packager.add('tpope/vim-repeat')
  call a:packager.add('tpope/vim-sensible')
  call a:packager.add('tpope/vim-surround')
  call a:packager.add('VoldikSS/vim-mma')
  call a:packager.add('w0rp/ale')
  " call a:packager.add('MikeCoder/markdown-preview.vim')
  " call a:packager.add('JamshedVesuna/vim-markdown-preview')
  " call a:packager.add('iamcco/markdown-preview.nvim', { 'do': { -> mkdp#util#install() } })
  " call a:packager.add('prabirshrestha/async.vim')
  " call a:packager.add('prabirshrestha/asyncomplete.vim')
  " call a:packager.add('prabirshrestha/asyncomplete-lsp.vim')
  " call a:packager.add('prabirshrestha/vim-lsp')
endfunction
packadd vim-packager
call packager#setup(function('s:packager_init'))
" }}}
" Vim Settings
" Vim Config {{{
" leader key
let mapleader = ','
let maplocalleader = '\'
" syntax
syntax on
" filetype
filetype on
filetype plugin on
filetype indent on
set backup
set autoread " auto reload files
set encoding=utf-8
set fileencodings=utf-8,gb2312,gb18030,gbk,ucs-bom,cp936,latin1
set spelllang=en,cjk
set clipboard=unnamedplus " use x11 normal clipboard
set autowrite " automatically write a file when leaving a buffer
set hidden
set laststatus=2
set timeoutlen=300 " time out for key code delays
set ttimeoutlen=0 " do not wait after escape key
set diffopt+=vertical " open diff window vertically
set completeopt=menuone,noinsert,preview " comletion
" }}}
" UI basic {{{
set guioptions=di " gui options: dark, icon
set number " show line number
set relativenumber " show relative number
set signcolumn=yes " always show sign column
set showcmd " shows what you are typing as a command
set wildmenu " turn on wild menu on :e <Tab>
set title " change the terminal's title
set showmatch " show matching characters
set scrolloff=2 " minimum lines above and below cursor
set splitright " split into right by default
set splitbelow " split to below by default, better for ale's hover info
set updatetime=100 " update time, related to fugitive sign column update, etc.
set lazyredraw " redraw when needed
set noerrorbells " don't beep
set shortmess+=I " don't show welcome screen
set showtabline=2 " show tabs / buffers on top
set noshowmode " do not show mode on the last line
set display+=lastline " show partial line
set background=dark " color scheme
set mouse=a " enable mouse support in console
" fix for urxvt and st
if !has('nvim')
    if $TERM =~ "rxvt-unicode"
        set ttymouse=urxvt
    elseif $TERM == "st" || $TERM == "st-256color"
        set ttymouse=sgr
    endif
endif
if $TERM != "linux"
    " cursor shape for vte compatible terminals (usrvt, st, xterm, termite)
    let &t_SI = "\e[6 q"
    let &t_SR = "\e[4 q"
    let &t_EI = "\e[2 q"
else
    " cursor shape for linux console
    let &t_ve = "\e[?25h"
    let &t_vi = "\e[?25l"
    let &t_SI = "\e[?0c"
    let &t_SR = "\e[?4c"
    let &t_EI = "\e[?8c"
endif
colorscheme slate
hi SignColumn   ctermfg=0   ctermbg=None
hi Search       ctermfg=0   ctermbg=3
hi IncSearch    ctermfg=0   ctermbg=3
hi Folded       ctermfg=8   ctermbg=None
hi SpellBad     ctermfg=9   ctermbg=None
hi SpellCap     ctermfg=11  ctermbg=None
hi Visual       ctermbg=0   ctermbg=None cterm=reverse
hi StatusLine   ctermfg=7   ctermbg=None cterm=None
hi StatusLineNC ctermfg=7   ctermbg=None cterm=None
hi VertSplit    ctermfg=7   ctermbg=None cterm=None
" Have to set styles for built-in targets related to tabbufline
hi TabLineSel   ctermfg=0   ctermbg=6    cterm=None
hi PmenuSel     ctermfg=6   ctermbg=None cterm=None
hi TabLine      ctermfg=7   ctermbg=None cterm=None
hi TabLineFill  ctermfg=7   ctermbg=None cterm=None
" incremental case-insensitive highlight smart case search
set incsearch ignorecase smartcase hlsearch
let g:termdebug_wide=1
" }}}
" Space Tabs Indentations {{{
" indentations
set autoindent
set smartindent
set cindent
" tabs
set tabstop=4
set shiftwidth=0  " make it the same as tabstop
set softtabstop=-1  " make it the same as shiftwidth
set expandtab
set smarttab
" show tabs and trailing white space
set list
set listchars=tab:>-,trail:.
" }}}
" Navigations {{{
" go up/down
nnoremap <silent> j gj
nnoremap <silent> k gk
vnoremap <silent> j gj
vnoremap <silent> k gk
" buffers
nnoremap <silent> <C-H> :bprevious<CR>
nnoremap <silent> <C-L> :bnext<CR>
" toggle fold
nnoremap <space> za
" center the screen when searching
nnoremap n nzzzv
nnoremap N Nzzzv
" 
noremap <leader>q :q<CR>
noremap <leader>Q :qa<CR>
noremap <leader>d :bd<CR>
noremap <leader>w :w<CR>
noremap <leader>e :e<CR>
noremap <leader>x :x<CR>
noremap <leader>k :call ExecCurrentFile()<CR>
noremap <leader>. :let @/=""<CR>
noremap <leader>r :so $MYVIMRC<CR>
noremap <leader>z :call Goyo()<CR>
" }}}
" Autocmd {{{
augroup normal
    autocmd!
    autocmd VimEnter * normal! :startinsert :stopinsert
    autocmd BufNewFile,BufRead *.mac setlocal ft=maxima
    autocmd BufNewFile,BufRead *.plt setlocal ft=gnuplot
    autocmd BufNewFile,BufRead muttrc,*muttrc setlocal ft=neomuttrc
    autocmd BufWritePost *Xresources silent call system("DISPLAY=:0 xrdb -load ".expand('%'))
    autocmd FileType tex,markdown,mma setlocal sw=2 ts=2 sts=2 cole=0
    autocmd FileType tex,markdown,gitcommit setlocal spell
    autocmd FileType gnuplot setlocal commentstring=#%s
    autocmd FileType maxima setlocal commentstring=/*%s*/
    autocmd FileType mma setlocal noet commentstring=(*%s*)
    autocmd VimEnter * if &diff | execute 'windo set wrap' | endif
augroup END
augroup terminal_setup
    autocmd!
    tnoremap <C-\> <C-\><C-N>
    if has('nvim')
        autocmd TermOpen term://* setlocal nonu nornu signcolumn=no
        autocmd TermOpen term://* startinsert
        autocmd TermClose term://* call feedkeys("\<CR>")
    else
        autocmd TerminalOpen * setlocal nonu nornu signcolumn=no
    endif
augroup END
" autocmd FileType tex let b:ale_lsp_root='./'
" }}}
" Fcitx Toggle {{{
" restore input method state when enter insert mode
let s:fcitx_cmd = executable("fcitx5-remote") ? "fcitx5-remote" : "fcitx-remote"
augroup fcitx
    autocmd!
    autocmd InsertLeave * let b:fcitx = system(s:fcitx_cmd) | call system(s:fcitx_cmd.' -c')
    autocmd InsertEnter * if exists('b:fcitx') && b:fcitx == 2 | call system(s:fcitx_cmd.' -o') | endif
augroup END
" }}}
" Netrw -> NerdTree {{{
" Make Netrw function like NerdTree
let g:netrw_home         = $VIM
let g:netrw_banner       = 0   " disable banner
let g:netrw_browse_split = 4   " open file in previous (CTRL-W_p) window
let g:netrw_winsize      = -20 " split size
let g:netrw_dirhistmax   = 0   " disable history
let g:netrw_liststyle    = 0   " thin listing
nnoremap <C-N> :Lexplore<CR>
" }}}
" My own goyo mode {{{
function Goyo()
    if exists("g:goyo_status")
        unlet g:goyo_status
        set nu rnu ru scl=auto stal=2 ls=2
    else
        let g:goyo_status = 1
        set nonu nornu noru scl=no stal=0 ls=0
    endif
endfunction
" }}}
" Execute current file {{{
function ExecCurrentFile()
    if &ft == "sh"
        execute ":!sh %:p"
    elseif &ft == "python"
        execute ":!python %:p"
    elseif &ft == "gnuplot"
        execute ":!gnuplot %:p"
    elseif &ft == "maxima"
        execute ":!maxima -b %:p"
    endif
    redraw!
endfunction
nmap <F5> :call ExecCurrentFile()<CR>
" }}}
" statusline {{{
augroup ALEProgress
  autocmd!
  autocmd User ALEJobStarted redrawstatus
  autocmd User ALELintPost   redrawstatus
  autocmd User ALEFixPost    redrawstatus
augroup END
function! ALEStatusLine()
    if get(g:, 'ale_enabled', 0) != 1
        return ''
    endif

    let l:ale_avail = get(g:, 'ale_enabled', 0) == 1
        \ && getbufvar(bufnr(''), 'ale_enabled', 1)
        \ && getbufvar(bufnr(''), 'ale_linted', 0) > 0
        \ && ale#engine#IsCheckingBuffer(bufnr('')) == 0
    let l:checking = ale#engine#IsCheckingBuffer(bufnr(''))
    let l:counts = ale#statusline#Count(bufnr(''))
    let l:all_errors = l:counts.error + l:counts.style_error
    let l:all_warnings = l:counts.warning + l:counts.style_warning

    if l:ale_avail == 0
        return ''
    elseif l:checking
        return ' ... '
    elseif l:counts.total == 0
        return ' OK '
    endif

    let l:ale_status = ' '
    let l:ale_status .= l:all_errors == 0 ? '' : printf('E:%2d ', l:all_errors)
    let l:ale_status .= l:all_warnings == 0 ? '' : printf('W:%2d ', l:all_warnings)
    let l:ale_status .= l:counts.info == 0 ? '' : printf('I:%2d ', l:counts.info)
    return l:ale_status
endfunction
function StatusLeftMiddle()
    if exists('*fugitive#head') == 1
        let l:githead = fugitive#head(6)
    else
        let l:githead = ''
    endif
    let l:readonly = &ro ? "[RO]" : ""
    let l:status = ' '
    let l:status .= len(l:githead) ? l:githead . ' ' : ""
    let l:status .= len(l:readonly) ? l:readonly . ' ' : ""
    return len(l:githead) + len(l:readonly) > 0 ? l:status : ""
    endif
endfunction
hi User1 ctermbg=0
hi User2 ctermbg=8
let &stl  = '%#TabLineSel# %{mode()} %1*'
let &stl .= '%2*%{StatusLeftMiddle()}%1*'
let &stl .= '%< %t %m %w '
let &stl .= '%='
let &stl .= ' %{&ff} | %{len(&fenc) > 0 ? &fenc : &enc} | %{len(&ft) > 0 ? &ft : "no ft"} '
let &stl .= '%2* %3p%% | %3l:%3c %1*'
let &stl .= '%#TabLineSel#%{ALEStatusLine()}%*'
" }}}
let fortran_free_source=1
" Plugin Settings
" {{{ buftabline
let g:buftabline_numbers = 2
let g:buftabline_indicators = 1
" }}}
" Vim Which Key {{{
let g:which_key_group_dicts = ''
let g:which_key_centered = 0
let g:which_key_hspace = 2
let g:which_key_use_floating_win = 0
let g:which_key_fallback_to_native_key = 1
let g:which_key_map = {
  \ 'q' : 'quit',
  \ 'Q' : 'quit all',
  \ 'd' : 'close buffer',
  \ 'w' : 'save',
  \ 'e' : 'edit (reload)',
  \ 'x' : 'save and quit',
  \ 'k' : 'run current file',
  \ '.' : 'clear search hl',
  \ 'z' : 'Goyo mode',
  \ 's' : 'slime',
  \ 'c' : 'comment',
  \ 'a' : { 'name' : '+ale' },
  \ 'g' : {
    \ 'name' : '+git-operation',
    \ 't' : { 'name' : '+stash' },
    \ },
  \ }
let g:which_key_map_local = {
  \ 'l' : { 'name' : '+vimtex' },
  \ }
if exists('*which_key#register')
    augroup vimtex
      autocmd!
      autocmd VimEnter * call which_key#register(mapleader, "g:which_key_map")
      autocmd VimEnter * call which_key#register(maplocalleader, "g:which_key_map_local")
    augroup end
    nnoremap <silent> <leader> :WhichKey '<leader>'<CR>
    vnoremap <silent> <leader> :WhichKeyVisual '<leader>'<CR>
    nnoremap <silent> <localleader> :WhichKey '<localleader>'<CR>
    vnoremap <silent> <localleader> :WhichKeyVisual '<localleader>'<CR>
    " vim unimpaired
    nnoremap <silent> [ :WhichKey '['<CR>
    nnoremap <silent> ] :WhichKey ']'<CR>
    nnoremap <silent> y :WhichKey 'y'<CR>
endif
" }}}
" Vim commentary {{{
nmap <leader>c <Plug>CommentaryLine
omap <leader>c <Plug>Commentary
xmap <leader>c <Plug>Commentary
" }}}
" Vim Endwise {{{
" https://github.com/tpope/vim-endwise/issues/41#issuecomment-432905813
autocmd FileType tex
  \ let b:endwise_addition = '\="\\end" . matchstr(submatch(0), "{.\\{-}}")' |
  \ let b:endwise_words = 'begin' |
  \ let b:endwise_pattern = '\\begin{.\{-}}' |
  \ let b:endwise_syngroups = 'texSection,texBeginEnd,texBeginEndName,texStatement'
" }}}
" Markdown {{{
" vim markdown
" fold
let g:markdown_folding = 1
let g:markdown_fenced_languages = ['html', 'python', 'bash=sh', 'sh', 'c']

let g:vim_markdown_folding_style_pythonic = 1
let g:vim_markdown_frontmatter = 1
let g:vim_markdown_new_list_item_indent = 2
" vim-markdown-preview
let vim_markdown_preview_browser='firefox'
" let vim_markdown_preview_use_xdg_open=1
let vim_markdown_preview_temp_file=1
let vim_markdown_preview_github=0
" preview
let g:mkdp_auto_start = 0
let g:mkdp_browser = 'firefox'
" }}}
" Fugitive {{{
" show the log in a pretty way
function! GitLog()
    silent Git log --all --decorate --oneline --graph --date-order
    redraw!
endfunction
function! GitTig()
    silent !tig -C "$(dirname "$(realpath "%")")"
    redraw!
endfunction
" key bindings
nnoremap <leader>gb :Gblame<CR>
nnoremap <leader>gd :Gvdiff<CR>
nnoremap <leader>gc :Gcommit -v<CR>
nnoremap <leader>gg :call GitTig()<CR>
nnoremap <leader>gs :Gstatus<CR>
nnoremap <leader>gl :call GitLog()<CR>
nnoremap <leader>gu :Gpush<CR>
nnoremap <leader>gf :Gpull<CR>
nnoremap <leader>gts :Git stash<CR>
nnoremap <leader>gtp :Git stash pop<CR>
nnoremap <leader>gtl :Git stash list<CR>
nmap <Nop>(g) <Plug>GetDiffCharPair
nmap <Nop>(p) <Plug>PutDiffCharPair
" }}}
" Gitgutter {{{
" the previous ~_ take two columns
let g:gitgutter_sign_modified_removed   = '^'
" update signs after focus
let g:gitgutter_terminal_reports_focus  = 0
let g:gitgutter_max_signs = 1000
" }}}
" ALE {{{
" automatically pop up completion candidates
let g:ale_completion_enabled = 1
let g:ale_completion_delay   = 0
" set sign column characters
let g:ale_sign_column_always   = 1
let g:ale_sign_error           = '>'
let g:ale_sign_warning         = '-'
" show pop up in balloons
let g:ale_set_balloons         = 1
let g:ale_set_quickfix         = 1
" only lint when leaving insert mode
let g:ale_lint_on_insert_leave = 1
let g:ale_lint_on_text_changed = 'normal'

" clangd needs a compile_commands.json file for the project
let g:ale_linters = {
  \ 'c': ['ccls'],
  \ 'cpp': ['ccls'],
  \ 'python': ['pylsp'],
  \ 'lua': ['luacheck'],
  \ 'sh': ['shellcheck'],
  \ 'tex': ['texlab', 'chktex'],
  \ 'fortran': ['gcc'],
  \ }
let g:ale_fixers = {
  \ '*': ['remove_trailing_lines', 'trim_whitespace'],
  \ 'c': ['clang-format', 'clangtidy'],
  \ 'cpp': ['clang-format', 'clangtidy'],
  \ 'sh': ['shellcheck']
  \ }
let g:ale_linters_explicit = 1
let g:ale_c_parse_makefile = 1

" Key mapping
nmap <leader>ar <Plug>(ale_find_references)
nmap <leader>ad <Plug>(ale_go_to_definition)
nmap <leader>an <Plug>(ale_next_wrap)
nmap <leader>ap <Plug>(ale_previous_wrap)
nmap <leader>aj <Plug>(ale_next_wrap)
nmap <leader>ak <Plug>(ale_previous_wrap)
nmap <leader>ah <Plug>(ale_hover)
nmap <leader>aR <Plug>(ale_rename)
" }}}
" Vim Tex {{{
let g:tex_flavor = 'latex'
" enable vimtex fold
let g:vimtex_fold_enabled = 1
let g:vimtex_syntax_enabled = 0
" start vim with a server, see ':h vimtex-clientserver'
if empty(v:servername) && exists('*remote_startserver') && !empty($DISPLAY) && empty($TMUX)
    call remote_startserver('VIM')
endif
" viewer setting
let g:vimtex_view_method = 'zathura'
" matching delimiters is causing performance issues
let g:vimtex_matchparen_enabled = 0
" compiler
let g:vimtex_compiler_method = "latexmk"
let g:vimtex_compiler_latexmk = {
    \ 'build_dir' : 'output',
    \}
augroup latex
    autocmd!
    autocmd FileType tex ab tp textperiodcentered{}
    autocmd FileType tex ab te textellipsis{}
    autocmd FileType tex ab op originalpage{}<Left>
    " add normal font in math
    autocmd FileType tex xnoremap <localleader>n s\mathrm{}<Esc>P
    autocmd FileType tex nnoremap <localleader>n a\mathrm{}<Left>
    " add red color, or to existing text
    autocmd FileType tex xnoremap <localleader>m s{\color{red}}<Esc>P
    autocmd FileType tex nnoremap <localleader>m a{\color{red}}<Left>
    " remove red color depends on vim-surround
    autocmd FileType tex map <localleader>d <Plug>Dsurround}d11l
    " autocmd FileType tex map <localleader>d ?\\color<CR>hmm%x`md12l
augroup END
" }}}
" Mathematica {{{
let g:mma_candy = 2
" }}}
" vim-slime {{{
let g:slime_no_mappings = 1
xmap <leader>s <Plug>SlimeRegionSend
nmap <leader>s <Plug>SlimeParagraphSend
" set config to use the pane on the right by default
let g:slime_default_config = {
    \ "socket_name": get(split($TMUX, ","), 0),
    \ "target_pane": "{right-of}"
    \ }
" vim terminal config
let g:slime_vimterminal_config = {
    \ "vertical": 1
    \ }
let g:slime_dont_ask_default = 0
" use temp file
let g:slime_paste_file = tempname()
" ipython specific setting
let g:slime_python_ipython = 0
" targets
if len($TMUX) != 0
    let g:slime_target = "tmux"
    let g:slime_dont_ask_default = 1
elseif match($TERM, "screen") != -1
    let g:slime_target = "screen"
else
    let g:slime_target = "vimterminal"
endif
" }}}
" vim-lsp {{{
" let g:which_key_map.l = { "name": "language server" }
" if executable('pyls')
"     " pip install python-language-server
"     au User lsp_setup call lsp#register_server({
"                 \ 'name': 'pyls',
"                 \ 'cmd': {server_info->['pyls']},
"                 \ 'whitelist': ['python'],
"                 \ })
" endif
" if executable('texlab')
"     au User lsp_setup call lsp#register_server({
"                 \ 'name': 'texlab',
"                 \ 'cmd': {server_info->['texlab']},
"                 \ 'whitelist': ['bib','tex'],
"                 \ })
" endif
" function! s:on_lsp_buffer_enabled() abort
"     setlocal omnifunc=lsp#complete
"     setlocal signcolumn=yes
" endfunction
" augroup lsp_install
"     au!
"     " call s:on_lsp_buffer_enabled only for languages that has the server registered.
"     autocmd User lsp_buffer_enabled call s:on_lsp_buffer_enabled()
" augroup END
" }}}
" asyncomplete {{{
" inoremap <expr> <Tab>   pumvisible() ? "\<C-n>" : "\<Tab>"
" inoremap <expr> <S-Tab> pumvisible() ? "\<C-p>" : "\<S-Tab>"
" inoremap <expr> <cr>    pumvisible() ? asyncomplete#close_popup() : "\<cr>"
" imap <c-space> <Plug>(asyncomplete_force_refresh)
" let g:asyncomplete_auto_popup = 1
" let g:asyncomplete_auto_completeopt = 0
" }}}
" ultisnippet {{{
let g:UltiSnipsJumpForwardTrigger="<Tab>"
let g:UltiSnipsJumpBackwardTrigger="<S-Tab>"
" }}}
" tagbar {{{
let g:tagbar_width = max([25, winwidth(0) / 5])
let g:tagbar_compact = 1
let g:tagbar_sort = 0
nnoremap <C-B> :Tagbar<CR>
" }}}
let g:mucomplete#enable_auto_at_startup = 1
let g:mucomplete#completion_delay = 1000
let g:mucomplete#chain = ['path', 'omni', 'spel']
" vim:foldmethod=marker:foldlevel=0
